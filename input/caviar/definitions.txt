> HoldsFor [close Id1 Id2 24 = true] I
	HoldsFor [distance Id1 Id2 24 = true] I

> HoldsFor [close Id1 Id2 25 = true] I
	HoldsFor [close Id1 Id2 24 = true] I1
	HoldsFor [distance Id1 Id2 25 = true] I2
	Union_All [I1 I2] I

> HoldsFor [close Id1 Id2 30 = true] I
	HoldsFor [close Id1 Id2 25 = true] I1
	HoldsFor [distance Id1 Id2 30 = true] I2
	Union_All [I1 I2] I

> HoldsFor [close Id1 Id2 34 = true] I
	HoldsFor [close Id1 Id2 30 = true] I1
	HoldsFor [distance Id1 Id2 34 = true] I2
	Union_All [I1 I2] I

> HoldsFor [close Id1 Id2 Threshold = false] I
	HoldsFor [close Id1 Id2 Threshold = true] I1
	Complement_All [I1] I

> HoldsFor [closeSymmetric Id1 Id2 Threshold = true] I
	HoldsFor [close Id1 Id2 Threshold = true] I1
	HoldsFor [close Id2 Id1 Threshold = true] I2
	Union_All [I1 I2] I

> InitiatedAt [person Id = true] T
	HappensAt Start [walking Id = true] T
	Not HappensAt [disappear Id] T

> InitiatedAt [person Id = true] T
	HappensAt Start [running Id = true] T
	Not HappensAt [disappear Id] T

> InitiatedAt [person Id = true] T
	HappensAt Start [active Id = true] T
	Not HappensAt [disappear Id] T

> InitiatedAt [person Id = true] T
	HappensAt Start [abrupt Id = true] T
	Not HappensAt [disappear Id] T

> InitiatedAt [person Id = false] T
	HappensAt [disappear Id] T

> InitiatedAt [leaving_object Person Object = true] T
	HappensAt [appear Object] T
	HoldsAt [inactive Object = true] T
	HoldsAt [person Person = true] T
	HoldsAt [closeSymmetric Person Object 30 = true] T

> InitiatedAt [leaving_object _Person Object = false] T
	HappensAt [disappear Object] T

> InitiatedAt [meeting P1 P2 = true] T
	HappensAt Start [ greeting1 P1 P2 = true] T
	Not HappensAt [disappear P1] T
	Not HappensAt [disappear P2] T

> InitiatedAt [meeting P1 P2 = true] T
	HappensAt Start [ greeting2 P1 P2 = true] T
	Not HappensAt [disappear P1] T
	Not HappensAt [disappear P2] T

> HoldsFor [greeting1 P1 P2 = true] I
	HoldsFor ! [activeOrInactivePerson P1 = true] IAI
	HoldsFor ! [person P2 = true] IP2
	HoldsFor ! [close P1 P2 25 = true] IC
	Intersect_All ! [IAI IC IP2] ITemp
	HoldsFor [running P2 = true] IR2
	HoldsFor [abrupt P2 = true] IA2
	Relative_Complement_All ITemp [IR2 IA2] I

> HoldsFor [greeting2 P1 P2 = true] I
	HoldsFor ! [walking P1 = true] IW1
	HoldsFor ! [activeOrInactivePerson P2 = true] IAI2
	HoldsFor ! [close P2 P1 25 = true] IC
	Intersect_All [IW1 IAI2 IC] I

> HoldsFor [activeOrInactivePerson P = true] I
	HoldsFor [active P = true] IA
	HoldsFor [inactive P = true] In
	HoldsFor [person P = true] IP
	Intersect_All [In IP] InP
	Union_All [IA InP] I

> InitiatedAt [meeting P1 _P2 = false] T
	HappensAt Start [running P1 = true] T

> InitiatedAt [meeting _P1 P2 = false] T
	HappensAt Start [running P2 = true] T

> InitiatedAt [meeting P1 _P2 = false] T
	HappensAt Start [abrupt P1 = true] T

> InitiatedAt [meeting _P1 P2 = false] T
	HappensAt Start [abrupt P2 = true] T

> InitiatedAt [meeting P1 P2 = false] T
	HappensAt Start [close P1 P2 34 = false] T

> HoldsFor [moving P1 P2 = true] MI
	HoldsFor [walking P1 = true] WP1
	HoldsFor [walking P2 = true] WP2
	Intersect_All ! [WP1 WP2] WI
	HoldsFor ! [close P1 P2 34 = true] CI
	Intersect_All [WI CI] MI

> HoldsFor [fighting P1 P2 = true] FightingI
    HoldsFor [abrupt P1 = true] AbruptP1I
	HoldsFor [abrupt P2 = true] AbruptP2I
	Union_All ! [AbruptP1I AbruptP2I] AbruptI
	HoldsFor ! [close P1 P2 24 = true] CloseI
	Intersect_All ! [AbruptI CloseI] AbruptCloseI
	HoldsFor [inactive P1 = true] InactiveP1I
	HoldsFor [inactive P2 = true] InactiveP2I
	Union_All [InactiveP1I InactiveP2I] InactiveI
	Relative_Complement_All AbruptCloseI [InactiveI] FightingI
